---
name: Basic Operators
menu: Guide
route: /guide/basic-operators
---

# Basic Operators

<!-- Unlike most languages, kaoscript supports four types of operators:

- Unary operators operate on a single operand (such as `-x`)
- Binary operators operate on two operands (such as `a += 42`)
- Ternary operators operate on three operands (such as `x ? 42 : 24`)
- Polyadic operators operator on infinite operands (such as `x + y + z` or `24 < x < 42`) -->

> :construction: kaoscript will allow to override operators.

## arithmetic

|      Operator      |      Meaning      |
|:------------------:|:-----------------:|
|     `-`*expr*      |     Negative      |
| *expr* `-` *expr*  |    Subtraction    |
| *expr* `+` *expr*  |     Addition      |
| *expr* `*` *expr*  |  Multiplication   |
| *expr* `/` *expr*  |     Division      |
| *expr* `/.` *expr* | Quotient division |
| *expr* `%` *expr*  |      Modulo       |

```kaoscript
// Numbers
1 + 2			// 3
1 + 2 + 3		// 6
1 - 2			// -1
2 * 3			// 6
18 / 3			// 6
28 / 5			// 5.6
28 /. 5			// 5
18 % 3			// 0
28 % 5			// 3
28 % 10 % 5		// 3
-2				// -2

// Strings
'foo' + 'bar'	// foobar
```

Unlike JavaScript, if an operand is `null`, the result will be `null`. And if an operand is not a number, an error will be thrown.

## comparison

|      Operator      |        Meaning        |
|:------------------:|:---------------------:|
| *expr* `==` *expr* |         Equal         |
| *expr* `!=` *expr* |       Not equal       |
| *expr* `>` *expr*  |     Greater than      |
| *expr* `<` *expr*  |       Less than       |
| *expr* `>=` *expr* | Greater than or equal |
| *expr* `<=` *expr* |  Less than or equal   |
| *expr* `~~` *expr* |       Matching        |
| *expr* `!~` *expr* |     Not matching      |
| *expr* `&` *expr*  |  Junctive AND (all)   |
| *expr* `|` *expr*  |   Junctive OR (any)   |
| *expr* `^` *expr*  |  Junctive XOR (one)   |

```kaoscript
1 == 1			// true
1 == 1 == 2		// false
1 != 2			// true
3 > 2 > 1		// true
3 < 5 < 1		// false
3 <= 3 <= 5		// true
4 >= 1			// true
```

### Chaining comparisons

Comparisons can be chained.

```kaoscript
1 < 2 <= 2 < 3 == 3 > 2 >= 1 == 1 < 3 != 5
// true

if 1 < x() < 42 {
	// `x()` is evaluated only once, its result is saved in a temporary variable
}
```

### Junctive operations

Both following statements are equivalent:

```kaoscript
if color == 'white' | 'black' | 'gray' | 'grey' {
}

if color == 'white' || color == 'black' || color == 'gray' || color == 'grey' {
}
```

## logical

The following operations are only valid for boolean operands.

|      Operator      | Meaning  |
|:------------------:|:--------:|
|     `!`*expr*      | Negation |
| *expr* `&&` *expr* |   And    |
| *expr* `||` *expr* |    Or    |
| *expr* `^^` *expr* |   Xor    |
| *expr* `->` *expr* |  Imply   |

```kaoscript
!true			// false
true || false	// true
true && false	// false
true ^^ false	// true
true -> false	// false
```

|  `x`  |  `y`  | `x || y` | `x && y` | `x ^^ y` | `x -> y` |
|:-----:|:-----:|:--------:|:--------:|:--------:|:--------:|
| false | false |  false   |  false   |  false   |   true   |
| false | true  |   true   |  false   |   true   |   true   |
| true  | false |   true   |  false   |   true   |  false   |
| true  | true  |   true   |   true   |  false   |   true   |

## bitwise & shift

The following operations are only valid for numeric operands.

|      Operator       |   Meaning   |
| :-----------------: | :---------: |
|      `+^`expr*      |     Not     |
| *expr* `+&` *expr*  |     And     |
| *expr* `+|` *expr*  |     Or      |
| *expr* `+^` *expr*  |     Xor     |
| *expr* `+<` *expr*  | Shift left  |
| *expr* `+>` *expr*  | Shift right |


```kaoscript
0x22 +& 0x0f    // 0x02
0x22 +| 0x0f    // 0x2f
0x22 +^ 0x0f    // 0x2d
0x22 +< 4       // 0x220
0x22 +> 4       // 0x02
```

## conditional

|           Operator           |      Meaning      |
| :--------------------------: | :---------------: |
|          `?`*expr*           |    Existential    |
|          `#`*expr*           |     Non empty     |
|      *expr* `??` *expr*      |  Null coalescing  |
|      *expr* `##` *expr*      | Empty coalescing  |
| *expr* `?` *expr* `:` *expr* | Ternary condition |

### Examples

```kaoscript
if ?value {
	// `value` is not null
}

t = x ?? y ?? z			// `t` is equal to the first not-null variable from `x`, `y` or `z` (in that order)

x = y == 42 ? 0 : 1		// `x` is equal to 0 is `y` equals `42`, if not `1`
```

The operator `??` is a polyadic operator.

### Distribution

| `x`  | `null`  |  `''`   | `'x'`  |  `[]`   | `[1,2,3]` |  `{}`   | `{ foo: 0 }` |
| :--: | :-----: | :-----: | :----: | :-----: | :-------: | :-----: | :----------: |
| `?x` | `false` | `true`  | `true` | `true`  |  `true`   | `true`  |    `true`    |
| `#x` | `false` | `false` | `true` | `false` |  `true`   | `false` |    `true`    |

## typing

|            Operator            |        Meaning        |
| :----------------------------: | :-------------------: |
|   *expr* `is` *limited-type*   |     Type matching     |
| *expr* `is not` *limited-type* |   Type not matching   |
|   *expr* `as` *limited-type*   |     Type casting      |
|  *expr* `as!` *limited-type*   |  Forced type casting  |
|  *expr* `as?` *limited-type*   | Nullable type casting |
|     *expr*`:`*entity-type*     |     Type casting      |
|    *expr*`:!`*entity-type*     |  Forced type casting  |
|    *expr*`:?`*entity-type*     | Nullable type casting |
|           *expr*`!?`           | Null removal matching |
|           *expr*`!!`           | Forced type matching  |

```kaoscript
class Foobar {

}

if x is Foobar {
	// `x`'s type is `Foobar`
}

if x is not Foobar {
	// `x`'s type isn't `Foobar`
}

t = (x as String).toUpperCase()

t = x:String.toUpperCase()

t = x as Array<String>
```

## assignment

|      Operator       |        Meaning         |
| :-----------------: | :--------------------: |
|  *expr* `=` *expr*  |       Assignment       |
| *expr* `<-` *expr*  | Assignment with return |
| *expr* `-=` *expr*  |      Subtraction       |
| *expr* `+=` *expr*  |        Addition        |
| *expr* `*=` *expr*  |     Multiplication     |
| *expr* `/=` *expr*  |        Division        |
| *expr* `/.=` *expr* |   Quotient division    |
| *expr* `%=` *expr*  |         Modulo         |
| *expr* `&&=` *expr* |      And logical       |
| *expr* `||=` *expr* |       Or logical       |
| *expr* `^^=` *expr* |      Xor logical       |
| *expr* `+&=` *expr* |      And bitwise       |
| *expr* `+|=` *expr* |       Or bitwise       |
| *expr* `+^=` *expr* |      Xor bitwise       |
| *expr* `+<=` *expr* |       Shift left       |
| *expr* `+>=` *expr* |      Shift right       |
| *expr* `?=` *expr*  |      Existential       |
| *expr* `!?=` *expr* |    Non existential     |
| *expr* `??=` *expr* |    Null coalescing     |
| *expr* `#=` *expr*  |       Non empty        |
| *expr* `!#=` *expr* |         Empty          |
| *expr* `##=` *expr* |    Empty coalescing    |

Unlike the assignment operator in JavaScript, the assignment operators don't return a value, except:
- `?=` and `#=` will return `true` when the assignment is **successful**. If not, `false`.
- `!?=` and `!#=` will return `true` when the assignment is **unsuccessful**. If not, `false`.
- `<-` is the same as `=` but returns the assigned value.

### Distribution

|    `x`    |    `y`    | `x ?= y` | `x ??= y` | `x #= y` | `x ##= y` |
| :-------: | :-------: | :------: | :-------: | :------: | :-------: |
|  `null`   |  `null`   |    ❌    |    ✅     |    ❌    |    ✅     |
|  `null`   |    `0`    |    ✅    |    ✅     |    ❌    |    ✅     |
|  `null`   | `[1,2,3]` |    ✅    |    ✅     |    ✅    |    ✅     |
|   `''`    |  `null`   |    ❌    |    ❌     |    ❌    |    ✅     |
|   `''`    |    `0`    |    ✅    |    ❌     |    ❌    |    ✅     |
|   `''`    |  `'abc'`  |    ✅    |    ❌     |    ✅    |    ✅     |
|   `''`    | `[1,2,3]` |    ✅    |    ❌     |    ✅    |    ✅     |
|  `'xyz'`  |  `null`   |    ❌    |    ❌     |    ❌    |    ❌     |
|  `'xyz'`  |    `0`    |    ✅    |    ❌     |    ❌    |    ❌     |
|  `'xyz'`  |  `'abc'`  |    ✅    |    ❌     |    ✅    |    ❌     |
|  `'xyz'`  | `[1,2,3]` |    ✅    |    ❌     |    ✅    |    ❌     |
|   `[]`    |  `null`   |    ❌    |    ❌     |    ❌    |    ✅     |
|   `[]`    |    `0`    |    ✅    |    ❌     |    ❌    |    ✅     |
|   `[]`    |  `'abc'`  |    ✅    |    ❌     |    ✅    |    ✅     |
|   `[]`    | `[1,2,3]` |    ✅    |    ❌     |    ✅    |    ✅     |
| `[4,5,6]` |  `null`   |    ❌    |    ❌     |    ❌    |    ❌     |
| `[4,5,6]` |    `0`    |    ✅    |    ❌     |    ❌    |    ❌     |
| `[4,5,6]` |  `'abc'`  |    ✅    |    ❌     |    ✅    |    ❌     |
| `[4,5,6]` | `[1,2,3]` |    ✅    |    ❌     |    ✅    |    ❌     |

- ✅ indicates that the assignment is **successful**
- ❌ indicates that the assignment is **unsuccessful**

## function

|         Operator          |               Meaning                |
| :-----------------------: | :----------------------------------: |
|  *expr*`(`*arguments*`)`  |         function application         |
| *expr*`?(`*arguments*`)`  |   conditional function application   |
| *expr*`^$(`*arguments*`)` |            curry function            |
| *expr*`^^(`*arguments*`)` |            curry function            |
| *expr*`*$(`*arguments*`)` |         function application         |
|    *expr* `|>` *expr*     |           forward pipeline           |
|    *expr* `<|` *expr*     |          backward pipeline           |
|    *expr* `|>?` *expr*    |  forward pipeline with null testing  |
|    *expr* `<|?` *expr*    | backward pipeline with null testing  |
|    *expr* `|>#` *expr*    | forward pipeline with empty testing  |
|    *expr* `<|#` *expr*    | backward pipeline with empty testing |
|    *expr* `*|>` *expr*    | forward pipeline with destructuring  |
|    *expr* `<|*` *expr*    | backward pipeline with destructuring |

## others

|      Operator       |          Meaning          |
| :-----------------: | :-----------------------: |
|   *expr*`.`*name*   |       member access       |
|  *expr*`?.`*name*   | conditional member access |
| *expr*`[`*expr*`]`  |       array access        |
| *expr*`?[`*expr*`]` | conditional array access  |
|      `.`*name*      |      implicit member      |
|      `**`*name*      |   default value if null   |

## rolling notation

### summary

- multi-lines only
- conditional expression
- operators:
	- `..`: rolling
	- `.`:  member access
	- `?.`: conditional member access
- a rolling notation must the the same class of operators (rolling operator or member access operators)

### basic example

```kaoscript
getAddress()
  ..setStreet('Elm', '13a')
  ..city = 'Carthage'
  ..state = 'Eurasia'
  ..zip(66666, extended: 6666)

// same as
var address = getAddress()
address.setStreet('Elm', '13a')
address.city = 'Carthage'
address.state = 'Eurasia'
address.zip(66666, extended: 6666)
```

### example with conditional expression

```kaoscript
var shape = Ellipse.new(10, 20)
	..rotation = 45 * Math.PI / 180 if filterRotate()
	..color = 'rgb(0,129,198)'
	..outlineWidth = 0

// same as
var shape = Ellipse.new(10, 20)

if filterRotate() {
	shape.rotation = 45 * Math.PI / 180
}

shape.color = 'rgb(0,129,198)'
shape.outlineWidth = 0
```

### example with existential test

```kaoscript
address?
	..setStreet('Elm', '13a')
	..city = 'Carthage'
	..state = 'Eurasia'
	..zip(66666, extended: 6666)

// same as
if ?address {
	address.setStreet('Elm', '13a')
	address.city = 'Carthage'
	address.state = 'Eurasia'
	address.zip(66666, extended: 6666)
}
```

### example with member access and conditional expression

```kaoscript
writer
	.code(' ')
	.code('*') if w
	.code('|>')
	.code('?') if q
	.code('#') if h
	.code(' ')

// same as
var mut writer2 = writer.code(' ')

if w {
	writer2 = writer2.code('*')
}

writer2 = writer2.code('|>')

if q {
	writer2 = writer2.code('?')
}
if h {
	writer2 = writer2.code('#')
}

writer2.code(' ')
```
