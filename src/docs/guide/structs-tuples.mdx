---
name: Structs & Tuples
menu: Guide
route: /guide/structs-tuples
---

# Structs & Tuples

## tuple

A tuple is a finite ordered list (sequence) of values (stored as an array).
The tuple's values can also be accessed by their names (if they aren't anonymous) or by their indexes.

### syntax

```syntax
[abstract] tuple *varname* [extends *varname*] [implements *varname1* ... [, *varnameN*]] "["
	*...element*
"]"

element = (
	:*type* [= *expression*]
	|
	*varname* (: *type* | ?) [= *expression*]
)
```

### example

```kaoscript
tuple Pair [ :String, :Number ]

var pair = Pair.new('x', 0.1)

console.log(`\(pair[0])`, pair[1] + 1)

pair[0] = 'foobar'
pair[1] = 3.14

pair.0 = 'foobar'
pair.1 = 3.14

console.log(`\(pair.0)`, pair.1 + 1)
```

### inheritance

```kaoscript
tuple Pair [
	x: String	= ''
	y: Number	= 0
]

tuple Triple extends Pair [
	z: Boolean	= false
]

var mut triple = Triple.new('x', 0.1, true)

console.log(`\(triple[0])`, triple[1] + 1, !triple[2])

console.log(`\(triple.0)`, triple.1 + 1, !triple.2)

console.log(`\(triple.x)`, triple.y + 1, !triple.z)

triple = Triple.new()
triple = Triple.new(z: true)
```

## struct

A struct is a group of variables (stored as object), only accessible by their names.

### syntax

```syntax
[abstract] struct *varname* [extends *varname*] [implements *varname1* ... [, *varnameN*]] {
	*...element*
}

element = *varname*[(: *type* | ?)] [= *expression*]
```

### example

```kaoscript
struct Point {
    x: Number
    y: Number
}

var mut point = Point.new(0.3, 0.4)

console.log(point.x + 1, point.x + point.y)

point.x = 3.14

console.log(point.x + 1, point.x + point.y)

point = Point.new(x: 0.3, y: 0.4)
```

### inheritance

```kaoscript
struct Point {
    x: Number
    y: Number
}

struct Point3D extends Point {
	z: Number	= 0
}

var mut point = Point3D.new(x: 0.3, y: 0.4)

console.log(point.x + 1, point.y + 2, point.z + 3)

point = Point3D.new(0.3, 0.4)
point = Point3D.new(0.3, 0.4, 1)
point = Point3D.new(x: 0.3, y: 0.4, z: 1)
```
