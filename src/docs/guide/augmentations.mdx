---
name: Augmentations
menu: Guide
route: /guide/augmentations
---

# Augmentations

## implements

`impl` statement allows the developer to augment the given class or namespace.

### syntax

```syntax
impl *varname* {
	(
		[private | internal | protected | public] (
			(override | overwrite) *method*
			|
			(override | overwrite) {
				*method*+
			}
			|
			(*variable* | *method*)
			|
			{
				(*variable* | *method*)+
			}
			|
			static (*static_variable* | *method*)
			|
			static {
				(*static_variable* | *method*)+
			}
			|
			lateinit {
				*lateinit_variable*+
			}
		)
	)*
}

variable = (
	auto [@]*varname* = *value*
	|
	const [@]*varname*[: *type*] = *value*
	|
	[@]*varname*[: *type*] [= *value*]
	|
	lateinit [const] [@]*varname*[: *type*]
)

static_variable = (
	auto [@]*varname* = *value*
	|
	const [@]*varname*[: *type*] = *value*
	|
	[@]*varname*[: *type*] [= *value*]
	|
	lateinit const [@]*varname*[: *type*]
)

lateinit_variable = (
	[const] [@]*varname*[: *type*] [= *value*]
	|
	auto [@]*varname* = *value*
)

method = [override | overwrite] [async] *varname*([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]])[: *type*] [~ *class1* [, *class2* ... [, *classN*]]] [ => *expression* | {
	*...statements*
}]
```

### example

```kaoscript
impl Shape {
	draw(shape): String {
		return `I'm drawing a \(@color) \(shape).`
	}
}
// adds the method 'draw(shape)' to the class 'Shape'

let s = new Shape('red')

console.log(s.draw())
// I'm drawing with a red pen

console.log(s.draw('circle'))
// I'm drawing a red circle.
```

## implements & native class

`impl` doesn't modify classes that have been flagged as `sealed`.

```kaoscript
extern sealed class Number
// import the external class 'Number' and flag it as `sealed` to avoid to directly modify it

impl Number {
	mod(max: Number): Number {
		if this == NaN {
			return 0
		}
		else {
			const n = this % max

			if n < 0 {
				return n + max
			}
			else {
				return n
			}
		}
	}
}
// adds the method 'mod' to the class 'Number'
// under the hood, the object `__ks_Number` is created to avoid to modify the object 'Number'

let i = 42
// inferred type: Number

console.log(i.mod(2))
// 0
// javascript code: console.log(__ks_Number._im_mod(i, 2))
```

You can add a new field to a native class. It won't modify the class but it will add the extra field to the instance of the class.

## override

The `override` keyword indicates that the method must inherit a method from the augmented class. If not, it will generate an error.
The overrinding method will have the same signature as the overridden method (same parameters, same return type and same throws).

```kaoscript
class LetterBox {
	format(message: String) => message.toUpperCase()
}

impl LetterBox {
	override format(message) => message.toLowerCase()
	// message is a String due to the overridden method
}
```

## overwrite

The `overwrite` keyword indicates that the method replace at least one method from the augmented class. If not, it will generate an error.
At the moment, the augmented class must be a `sealed` class.

The new method replaces all the methods that its parameters can match.

```kaoscript
extern sealed class Date {
    setDate(value: Number): Number
}

impl Date {
    overwrite setDate(value: Number): Date {
        precursor(value)

        return this
    }
}
```

> :construction: If the method is directly replaced in a non-sealed class, the signature won't match for its internal usage which can generate runtime error. To avoid this, the new method must be generated like a method of a sealed class. So the non-sealed class would require an half-sealed state.