---
name: Flow Statements
menu: Guide
route: /guide/flow-statements
---

# Flow Statements

> Any tests require a boolean.

If the test can't be a boolean, an error will thrown at compilation time.
If the type is unknow, the test's value must be equal to `true`.

## if...else

### Syntax

```syntax
if
	*expression:Boolean*
	|
	var [mut] (*varname* | *destructuring*) [: *type*] (?= | #=) *expression*
{
	*...statements*
}
[else if
	*expression:Boolean*
	|
	var [mut] (*varname* | *destructuring*) [: *type*] (?= | #=) *expression*
{
	*...statements*
}]
[else {
	*...statements*
}]
```

### Examples

```kaoscript
if x == 5 {
}
else if var x ?= getX() {
	// x is:
	// - immutable
	// - not null
}
else {
}
```

## unless

`unless x` is equivalent to `if !x`.

### Syntax

```syntax
unless *expression:Boolean* {
	*...statements*
}
```

### Examples

```kaoscript
unless x == 0 {
}
```

## for...from

### Syntax

```syntax
for
	[var [mut]] *varname*
	from *expression:Number*
	(til | to) *expression:Number*
	[by *expression:Number*]
	[(while | until) *expression:Boolean*]
	[when *expression:Boolean*]
{
	*...statements*
}
```

### Examples

```kaoscript
for x from 0 to 10 {
	console.log(x)
}
// 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10

for var x from 0 til 10 {
	console.log(x)
}
// 0, 1, 2, 3, 4, 5, 6, 7, 8, 9
```

## for...in

Unlike JavaScript, in kaoscript, `for...in` loops an array.

### Syntax

```syntax
for
	[var [mut]] (*varname* | *destructuring* | '_') [: *type*] [, *index*]
	in *expression:Array*
	[desc]
	[from *expression:Number*]
	[(til | to) *expression:Number*]
	[by *expression:Number*]
	[(while | until) *expression:Boolean*]
	[when *expression:Boolean*]
{
	*...statements*
}
```

### Examples

```kaoscript
heroes = ['leto', 'duncan', 'goku']

for var hero, index in heroes when index % 2 == 0 {
	console.log(hero)
}
// leto, goku
```

```kaoscript
heroes = ['leto', 'duncan', 'goku']

for hero in heroes until hero == 'duncan' {
	console.log(hero)
}
// leto
```

## for...of

Unlike JavaScript, in kaoscript, `for...in` loops an object.

### Syntax

```syntax
for
	[var [mut]] (*varname* | *destructuring* | '_') [: *type*] [, *key*]
	of *expression:Object*
	[(while | until) *expression:Boolean*]
	[when *expression:Boolean*]
{
	*...statements*
}
```

### Examples

```kaoscript
likes = {
	leto: 'spice'
	paul: 'chani'
	duncan: 'murbella'
}

for value, key of likes {
	console.log(`\(key) likes \(value)`)
}
// leto likes spice
// paul likes chani
// duncan likes murbella
```

## for...range

### Syntax

```syntax
for
	[var [mut]] (*varname* | '_') [, *index*]
	in *operand:Number*[<]..[<]*operand:Number*[..*operand:Number*]
	[(while | until) *expression:Boolean*]
	[when *expression:Boolean*]
{
	*...statements*
}
```

### Examples

```kaoscript
for x in 0..10..2 while test(x) {
}
```

## while & do...while

`while` executes the loop while the condition is `true`.

### Syntax

```syntax
while
	*expression:Boolean*
	|
	var [mut] (*varname* | *destructuring*) [: *type*] (?= | #=) *expression*
{

}
```

```syntax
do  {

}
while *expression:Boolean*
```

### Examples

```kaoscript
while supply > demand {
	buy()
}
```

```kaoscript
do {
	buy()
}
while supply > demand
```

## until & do...until

`until` executes the loop and stops it when the condition becomes `true`.

### Syntax

```syntax
until *expression:Boolean* {

}
```

```syntax
do  {

}
until *expression:Boolean*
```

### Examples

```kaoscript
until supply > demand {
	sell()
}
```

```kaoscript
do {
	sell()
}
until supply > demand
```

## switch

Unlike JavaScript, in kaoscript, `switch` won't automatically cascade.
It executes only and only one branch. (No `break` needed)

### Syntax

```syntax
switch *expression* {
	[*expression1*[, *expression2* ... [*expressionN*]] | is *class* | '_']
	[with (*varname* [as *type*] | *destructuring*) ]
	[when *expression:Boolean*]
	=> (
		*statement*
		| {
			*...statements*
		}
	)
}
```

### Examples

```kaoscript
switch number {
	1               => console.log("One!")
	2, 3, 5, 7, 11  => console.log("This is a prime")
	13..19          => console.log("A teen")
	_               => console.log("Ain't special")
}
```

```kaoscript
switch age() {
	0					=> console.log(`I'm not born yet I guess`)
	1  .. 12 	with n	=> console.log(`I'm a child of age \(n)`)
	13 .. 19 	with n	=> console.log(`I'm a teen of age \(n)`)
				with n	=> console.log(`I'm an old person of age \(n)`)
}
```

```kaoscript
var somePoint = [1, 1]

switch somePoint {
	[0, 0]			=> console.log(`(0, 0) is at the origin`)
	[_, 0]			=> console.log(`(\(somePoint[0]), 0) is on the x-axis`)
	[0, _]			=> console.log(`(0, \(somePoint[1])) is on the y-axis`)
	[-2..2, -2..2]	=> console.log(`(\(somePoint[0]), \(somePoint[1])) is inside the box`)
	_				=> console.log(`(\(somePoint[0]), \(somePoint[1])) is outside of the box`)
}
```

```kaoscript
var somePoint = [1, 1]

switch somePoint {
	with [x, y] when x == 0 && y == 0				=> console.log(`(0, 0) is at the origin`)
	with [x, y] when y == 0							=> console.log(`(\(x), 0) is on the x-axis`)
	with [x, y] when x == 0							=> console.log(`(0, \(y)) is on the y-axis`)
	with [x, y] when -2 <= x <= 2 && -2 <= y <= 2	=> console.log(`(\(x), \(y)) is inside the box`)
	with [x, y]										=> console.log(`(\(x), \(y)) is outside of the box`)
	_												=> console.log(`Not a point`)
}
```

```kaoscript
var value = {
	foo: 1,
	bar() => 2
}

switch value {
	{foo: 1}	with {qux: n} 			=> console.log(`qux: \(n)`)
	{foo: 1} 							=> console.log('foo: 1')
	{foo}								=> console.log('has foo')
	{qux}								=> console.log('has qux')
	_			when value.bar() == 0	=> console.log('bar() == 0')
	_									=> console.log('oops!')
}
```

```kaoscript
switch view {
	is UIImageView								=> console.log("It's an image view")
	is UILabel		with label as UILabel		=> console.log("It's a label")
	is UITableView	with tblv as UITableView	=> {
						var sectionCount = tblv.numberOfSections()
						console.log(`It's a table view with \(sectionCount) sections`)
					}
	_											=> console.log("It's some other UIView or subclass")
}
```

### fallthrough

For cascading, the `fallthrough` statement is required

```kaoscript
func foobar(d) {
	switch d {
		'hour' => {
			console.log('hour')

			fallthrough
		}
		'minute' => {
			console.log('minute')

			fallthrough
		}
		'second' => {
			console.log('second')
		}
	}
}
```

## break & continue

### break

The `break` statement ends execution of an entire control flow statement immediately.

### continue

The `continue` statement tells a loop to stop what it is doing and start again at the beginning of the next iteration through the loop.

## with

### Syntax

```syntax
with *var-init* {
	...statements
}
[finally {
	...statements
}]

with {
	(*var-init*)+
}
then {
	...statements
}
[finally {
	...statements
}]

var-init = (
	var [mut] *varname* = *expression*
	|
	*variable* (=|+=|-=|\*=|/=|/.=|%=|&&=|"||="|^^=|<<=|>>=) *expression*
)

variable = [@]*varname*(\.*varname* | "[" *expression* "]")\*
```

### Examples

```kaoscript
with var file = open() {
	var text = async file.readText()
}
finally {
	file.close()
}

with {
	var file = open()
	var file2 = open2()
}
then {
	var text = async file.readText()
}
finally {
	file.close()
}
```

```kaoscript
var mut mode = 2

with mode += 12 {
	// mode = 14
}

// mode = 2
```

## pass

The `pass` statement indicates that the block should be empty. No others statements should be around that statement.

It can be useful to split complicated condition into more readable `if/else` statements and a branch needs to be empty.

### Syntax

```syntax
pass
```
