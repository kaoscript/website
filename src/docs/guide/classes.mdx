---
name: Classes
menu: Guide
route: /guide/classes
---

# Classes

## syntax

```syntax
[abstract] class *varname*["<" *type* ">"][@ *version*] [extends *varname*] {
	(
		[private | protected | public] (*field* | *method*)
		|
		(private | protected | public) {
			(*field* | *method*)+
		}
	)*
}

field = *varname*[: *type*] [= *value*]

method = [abstract | override] [async] *varname*([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]])[: *type*] [~ *class1* [, *class2* ... [, *classN*]]] [ => *expression* | {
	*...statements*
}]
```

## example

```kaoscript
class Shape {
	private {
		_color: String
	}

	draw(): String {
        return `I'm drawing with a \(this._color) pen.`
    }
}
```

## this aliasing

`@x` is an alias for `this.x`, `this._x` or even `this.x()`.

```kaoscript
class Shape {
	private {
		@color: String
		// same as: _color: String
	}

	draw(): String {
        return `I'm drawing with a \(@color) pen.`
    }
}
```

## constructor

The constructor is called when creating a new object like `new Shape()`

```kaoscript
class Shape {
	private _color: String

    constructor(@color)
	// automatically set the instance variable '_color' as the parameter 'color'
}
```

## destructor

The destructor is called by the statement `delete`.

```kaoscript
extern console

class Shape {
	private {
		_color: String
	}

    constructor(@color)

	destructor() {
		@color = null
	}
}

let s = new Shape('red')

delete s
// the destructor is called and the variable `s` is unreferenced
```

## class members

Class members are declared with the modifier `static`.

```kaoscript
class Rectangle {
	public static shape() => 'Rectangle'

	static {
		color() {
			return '1.0.0'
		}
	}
}

console.log(Rectangle.shape(), Rectangle.color())
```

## superclass & subclass

```kaoscript
extern console

class Shape {
	private {
		_color: String
	}

    constructor(@color)
	// automatically set the instance variable '_color' as the parameter 'color'

	destructor() {
		@color = null
	}

	color() => @color
	color(@color) => this

    draw(): String {
        return `I'm drawing with a \(@color) pen.`
    }
}

class Rectangle extends Shape {
    draw() {
        return `\(super()) I'm drawing a \(@color) rectangle.`
    }
}

let r = new Rectangle('black')

console.log(r.draw())
// I'm drawing with a black pen. I'm drawing a black rectangle.
```

## override

The `override` keyword indicates that the method must inherit a method from the superclass. If not, it will generate an error.

```kaoscript
class Shape {
	draw(): String {
        return `I'm drawing a shape.`
    }
}

class Rectangle extends Shape {
	override draw() {
        return `I'm drawing a rectangle.`
    }
}
```

If a parameter type, return type or error type is missing, it will be replaced but the type of the overridden type.
If the type isn't missing, it will need to be equal to the overridden type.

<!-- ## final

The `final` keyword indicates that the class or the method is final. It can't be extended or augmented.

```kaoscript
final class Shape {
	draw(): String {
        return `I'm drawing a shape.`
    }
}

class Rectangle extends Shape {
}
// will throw an error
``` -->

## abstract

```kaoscript
abstract class AbstractGreetings {
	private {
		_message: String: ''
	}

	constructor() {
		this('Hello!')
	}

	constructor(@message)

	abstract greet(name): String
}

class Greetings extends AbstractGreetings {
	greet(name) => `\(@message)\nIt's nice to meet you, \(name).`
}
```

## versioning

kaoscript classes can be signed with a version which could be used for serialization.

```kaoscript
class Rectangle@1.0.0 {
    private {
    	_color: String
    }

    constructor(@color = 'black')

    draw(canvas) {
        return `I'm drawing a \(@color) rectangle.`
    }
}

console.log(Rectangle.name)
// Rectangle
console.log(Rectangle.version)
// 1.0.0
```