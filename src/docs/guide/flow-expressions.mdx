---
name: Flow Expressions
menu: Guide
route: /guide/flow-expressions
---

# Flow Expressions

## array comprehensions

### Syntax

```syntax
"["
	*expression*
	(
		for
		(
			[var] *varname*
				from *expression:Number*
				(til | to) *expression:Number*
				[step *expression:Number*]
				[(while | until) *expression:Boolean*]
				[when *expression:Boolean*]
			|
			[var] (*varname* | *destructuring* | '\_') [: *type*] [, *index*]
				in *expression:Array*
				[from *expression:Number*]
				[down]
				[(to | to~) *expression:Number*]
				[step *expression:Number*]
				[split *expression:Number*]
				[(while | until) *expression:Boolean*]
				[when *expression:Boolean*]
			|
			[var] (*varname* | *destructuring* | '\_') [: *type*] [, *index*]
				of *expression:Object*
				[(while | until) *expression:Boolean*]
				[when *expression:Boolean*]
			|
			[var] *varname*[, *index*] | :*index*
				in *operand:Number*[<]..[<]*operand:Number*[..*operand:Number*]
				[(while | until) *expression:Boolean*]
				[when *expression:Boolean*]
		)
		|
		repeat *expression* times
	)
"]"
```

### Examples

```kaoscript
likes = {
	leto: 'spice'
	paul: 'chani'
	duncan: 'murbella'
}

spicyHeroes = [hero for like, hero of likes when like == 'spice']
// spicyHeroes = ['leto']
```

```kaoscript
a = [i for var i from 0 to 10 by 2 until foo(i)]
```

## if...else

### Examples

```kaoscript
var x = if foo() {
	pick qux()
}
else {
	pick bar()
}

var v1 = v2.substring(
	v2.lastIndexOf(
		c1
		if v2.contains(c2) {
			pick v2.indexOf(c2)
		}
		else {
			pick v2.length
		}
	) + 1
	if v2.contains(c2) {
		pick v2.indexOf(c2, v2.lastIndexOf(c1, v2.indexOf(c2)))
	}
	else {
		pick v2.length
	}
)

// same as
var v1 = v2.substring(
    v2.lastIndexOf(c1, v2.contains(c2) ? v2.indexOf(c2) : v2.length) + 1
    v2.contains(c2) ? v2.indexOf(c2, v2.lastIndexOf(c1, v2.indexOf(c2))) : v2.length
)
```

### Syntax

The inline `if` expression is using the same syntax as an `if` statement but has 2 caveats:
- all cases must be covered
- use `pick` statement to "return" a value to the root expression

## match

### Examples

```kaoscript
func color(fg: ANSIColor, bg: ANSIColor): String {
	var fgCode = match fg {
		.black		=> 30
		.red		=> 31
		.green		=> 32
		.yellow		=> 33
		.blue		=> 34
		.magenta	=> 35
		.cyan		=> 36
		.white		=> 37
		else		=> 39
	}

	...

	return `\(fgCode);\(bgCode)m`
}
```

### Syntax

The inline `match` expression is using the same syntax as an `match` statement but has 2 caveats:
- all cases must be covered
- use `pick` statement to "return" a value to the root expression

## in assignment statement

### Syntax

```syntax
... = *expression* [
	if *expression:Boolean*
	|
	unless *expression:Boolean*
]
```

### Examples

```kaoscript
y = 10 if x == 5
```

## in expression statement

### Syntax

```syntax
*expression* [
	for
		(
			[var] *varname*
				from[~] *expression:Number*
				[(down | up)]
				to[~] *expression:Number*
				[step *expression:Number*]
				[(while | until) *expression:Boolean*]
				[when *expression:Boolean*]
			|
			[var] (*varname* | *destructuring* | '\_') [: *type*] [, *index*]
				in *expression:Array*
				[from[~] *expression:Number*]
				[(down | up)]
				[to[~] *expression:Number*]
				[step *expression:Number*]
				[(while | until) *expression:Boolean*]
				[when *expression:Boolean*]
			|
			[var] (*varname* | *destructuring* | '\_') [: *type*] [, *index*]
				of *expression:Object*
				[(while | until) *expression:Boolean*]
				[when *expression:Boolean*]
			|
			[var] *varname*[, *index*] | :*index*
				in *operand:Number*[<]..[<]*operand:Number*[..*operand:Number*]
				[(while | until) *expression:Boolean*]
				[when *expression:Boolean*]
		)
	|
	repeat *expression* times
	|
	if *expression:Boolean*
	|
	unless *expression:Boolean*

]
```

### Examples

```kaoscript
foobar() if x == 5
```

```kaoscript
heroes = ['leto', 'duncan', 'goku']

console.log(hero) for hero in heroes
```

## in return statement

### Syntax

```syntax
return [
	if *expression:Boolean*
	|
	unless *expression:Boolean*
	|
	*expression* [
		if *expression:Boolean*
		|
		unless *expression:Boolean*
	]
]
```

### Examples

```kaoscript
return if lang == 'en'
```

## in throw statement

### Syntax

```syntax
throw *expression* [
	if *expression:Boolean*
	|
	unless *expression:Boolean*
]
```

### Examples

```kaoscript
throw Error.new('Not Defined') if !?x
```

## in object expression

### Syntax

```syntax
{
	*varname*[: *expression*]
	if *expression:Boolean*
	|
	unless *expression:Boolean*
}
```

### Examples

```kaoscript
var point = {
	x: 1
	y: 1
	z: 1 if _3d
}
```

## in named argument expression

### Syntax

```syntax
'('
	(*varname*: *expression* | :*varname*)
	if *expression:Boolean*
	|
	unless *expression:Boolean*
')'
```

### Examples

```kaoscript
foobar(
	x: 1
	y: 1
	z: 1 if _3d
)
```

## in cascade expression

### Syntax

```syntax
*expression* (if|unless) *expression:Boolean*$
```

### Examples

```kaoscript
writer
	.newLine()
	.code('break')
	.code(' ').expression(data.label) if ?data.label
	.done()
```
