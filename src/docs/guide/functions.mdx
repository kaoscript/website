---
name: Functions
menu: Guide
route: /guide/functions
---

# Functions

In kaoscript, there is three types of functions:
- named functions
- anonymous functions
- arrow functions

|                                 | named function | anonymous function | arrow function |
|:-------------------------------:|:--------------:|:------------------:|:--------------:|
|        new local `this`         |      YES       |        YES         |                |
| local `this` is parent's `this` |                |                    |      YES       |
|    access module's variables    |      YES       |        YES         |      YES       |
|    access parent's variables    |                |                    |      YES       |
|          overload-able          |      YES       |                    |                |
|           async-able            |      YES       |        YES         |      YES       |
|           return type           |      YES       |        YES         |      YES       |
|           error types           |      YES       |        YES         |      YES       |
|            statement            |      YES       |                    |                |
|           expression            |                |        YES         |      YES       |

## named function

### Syntax

```syntax
[async] func *varname*([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]])[: *type* | auto] [~ *class1* [, *class2* ... [, *classN*]]] [ => *expression* | {
	*...statements*
}]
```

### Examples

```kaoscript
import '@zokugun/lang'

func alpha(n: Number, percentage = false): Number {
	var i = n.toInt()

	return 1 if i == NaN else (percentage ? i / 100 : i).limit(0, 1).round(3)
}

func red() => '#FF0000'
```

## anonymous function

### Syntax

```syntax
[async] func([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]])[: *type*] [~ *class1* [, *class2* ... [, *classN*]]] [ => *expression* | {
	*...statements*
}]
```

### Examples

```kaoscript
foobar(func(a:Number, b:Number) {
	return a - b
})

foobar(func(a:Number, b:Number): Number => a - b)
```

## arrow function

### Syntax

```syntax
[async] [([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]])[: *type*] [~ *class1* [, *class2* ... [, *classN*]]] | *parameter* ] => [ *expression* | {
	*...statements*
}]
```

### Examples

```kaoscript
var foo = (a:Number, b:Number) => a - b

var four = (a => a / 10)(42)
```

## parameters

### Syntax

```syntax
[mut] (*varname* | *destructuring* | ...[arity][*varname*] | '_') [!] [: *type*] [?] [= *expression*]

arity = { ([*min*], [*max*] | *quantity*) }
```

### Examples

```kaoscript
require expect: func

func foo(u = null, v, x, y = null, z = null) {
	return [u, v, x, y, z]
}

expect(() => foo()).to.throw()

expect(() => foo(1)).to.throw()

expect(foo(1, 2)).to.eql([null, 1, 2, null, null])

expect(foo(1, 2, 3)).to.eql([1, 2, 3, null, null])

expect(foo(1, 2, 3, 4)).to.eql([1, 2, 3, 4, null])

expect(foo(1, 2, 3, 4, 5)).to.eql([1, 2, 3, 4, 5])
```

### Distribution

|                               | Required | Nullable | Typed | Default Value |
| ----------------------------- |:--------:|:--------:|:-----:|:-------------:|
| `foo(x)`                      |   YES    |          |       |               |
| `foo(x?)`                     |   YES    |   YES    |       |               |
| `foo(x = null)`               |          |   YES    |       |      YES      |
| `foo(x = 'foobar')`           |          |          |       |      YES      |
| `foo(x? = 'foobar')`          |          |   YES    |       |      YES      |
| `foo(x! = null)`              |   YES    |   YES    |       |      YES      |
| `foo(x! = 'foobar')`          |   YES    |          |       |      YES      |
| `foo(x!? = 'foobar')`         |   YES    |   YES    |       |      YES      |
| `foo(x: String)`              |   YES    |          |  YES  |               |
| `foo(x: String?)`             |   YES    |   YES    |  YES  |               |
| `foo(x: String = null)`       |          |   YES    |  YES  |      YES      |
| `foo(x: String = 'foobar')`   |          |          |  YES  |      YES      |
| `foo(x: String? = null)`      |          |   YES    |  YES  |      YES      |
| `foo(x: String? = 'foobar')`  |          |   YES    |  YES  |      YES      |
| `foo(x!: String = null)`      |   YES    |   YES    |  YES  |      YES      |
| `foo(x!: String = 'foobar')`  |   YES    |          |  YES  |      YES      |
| `foo(x!: String? = null)`     |   YES    |   YES    |  YES  |      YES      |
| `foo(x!: String? = 'foobar')` |   YES    |   YES    |  YES  |      YES      |

## rest parameter

Unlike JavaScript, in kaoscript, the `rest` parameter is allowed in any position.

### Examples

```kaoscript
func foo(...items) {
	console.log(items)
}

func quux(x = 1, ...items, z = 1) {
	console.log(x, items, z)
}

func corge(...items = [1..5]) {
	console.log(items)
}
```

### Type

The given type of a rest parameter is the type of its elements.

```kaoscript
func foobar(...args: Number) {
	// args is Array<Number>
	for var arg in args {
		// arg is Number
	}
}
```

### Arity

The `rest` parameter can have arity like:

```kaoscript
func foo(...{1,3}args) {
// args will contain between 1 and 3 elements
}

func bar(...{3,}args) {
// args will contain at least 3 elements
}

func quux(...{,3}args) {
// args will contain at most 3 elements
}

func corge(...{3}args) {
// args will contain 3 elements
}
```

## anonymous parameter

In kaoscript, a parameter can be ignored with the placeholder `_` (can be ignored for a rest parameter).
Even if a parameter is ignored, it will be validated (`_` won't accept `null`, whereas `_?` will)

### Examples

```kaoscript
func foo(...) {
}

func bar(_, x, y) {
	console.log(x, y)
}
```

## asynchronous

`async/await` use the traditional `callback(error, ...values)` where the `error` can be catched with `try/catch`.

### Examples

```kaoscript
import '@zokugun/lang'
import 'child_process' for exec

var df_regex = /([\/[a-z0-9\-\_\s]+)\s+([0-9]+)\s+([0-9]+)\s+([0-9]+)\s+([0-9]+%)\s+([0-9]+)\s+([0-9]+)\s+([0-9]+%)\s+(\/.*)/i

async func disks() {
	var stdout: string, stderr = await exec('df -k')

	var disks = []

	for var line in stdout.lines() {
		if var matches = df_regex.exec(line) {
			disks.push({
				device: matches[1].trim(),
				mount: matches[9],
				total: matches[2].toInt() * 1024,
				used: matches[3].toInt() * 1024,
				available: matches[4].toInt() * 1024
			})
		}
	}

	return disks
}

var d = await disks()
```

## return type

### Syntax

```syntax
: *type*
```

### Examples

```kaoscript
func foobar(): Number => 42
```

### auto

With `auto`, kasocript will automatically determine the return type

```kaoscript
func foobar(): auto => 42
// return type: Number
```

### void

```kaoscript
func foobar(): void {
}
```

`void` indicates that the function won't ever return any values. So it can never be an operand.
The `void` type can only be used as a return type, it doesn't have any meaning in others contexts.

### never

```kaoscript
func foobar(): never {
	throw new Error()
}
```

`never` indicates that the function will never return, it will always generate an error.
As for `void`, `never` can only be used as a return type.

The previous code is equivalent to:

```kaoscript
func foobar(): never ~ Error {
	throw new Error()
}
```

`~ Error` can be omitted since `never` is already indicating that the function will throw an error.

## error handling

Like Java, in kaoscript, a function exposes the list of possible errors. Those errors will have to be handled by the caller. (This behaviour can be disabled with an attribute.)


### Syntax

```syntax
~ *class1* [, *class2* ... [, *classN*]]
```

### Examples

```kaoscript
func foobar() ~ Error {
}

func handled() {
	try {
		foobar()
	}
}

func passed() ~ Error {
	foobar()
}

#[error(off)]
func disabled() {
	foobar()
}

#[error(ignore(Error))]
func filtered() {
	foobar()
}
```

## overloading

kaoscript allows named functions to be overloaded.

### Examples

```kaoscript
func reverse(value: String): String => value.split('').reverse().join('')
func reverse(value: Array): Array => value.slice().reverse()

reverse('foobar')
// 'raboof'

reverse([1, 2, 3, 4])
// [4, 3, 2, 1]
```

## currying

|      |     `this` is     |
|:----:|:-----------------:|
| `^$` |  first argument   |
| `^^` |      `null`       |
| `^@` | function's `this` |

### argument

```kaoscript
var log = console.log^$(console, 'hello:')

log('foo')
// hello: foo
```

### null

```kaoscript
func log(...args) {
	console.log(...args)
}

var logHello = log^^('hello:')

logHello('foo')
// hello: foo
```

### self

```kaoscript
var log = console.log^@('hello:')

log('foo')
// hello: foo
```

## calling

|      |     `this` is     |
|:----:|:-----------------:|
| `*$` |  first argument   |
| `**` |      `null`       |

### argument

```kaoscript
func log(...args) {
	this.log(...args)
}

log*$(console, 'hello')
```

### null

```kaoscript
func log(...args) {
	console.log(...args)
}

log**('hello')
```
