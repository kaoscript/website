---
name: Variable Declarations
menu: Guide
route: /guide/variable-declarations
---

# Variable Declarations

## var

By default, a variable is **immutable**.

### Description

```kaoscript
var name = 'John'
```

`var` declares the variable `name` as **immutable reference**. But the referenced value can be modified.

```kaoscript
var names = ['John']

names.push('Jane')
// names: ['John', 'Jane']

// but
names = ['John']
// will through an error
```

### Typing

If not type is provided, `var` will deduce the variable's type based on its value.

```kaoscript
var name = 'John'
// type: String

var names: Array = []
// type: Array
```

The type of a variable declared with `var` can't be changed.

## var mut

With `var mut`, the variable is **mutable** but still **typed**

### Typing

If a type is provided at the declaration of a variable, its type can't be changed.

```kaoscript
var mut name: String = 'John'

name = 42
// throws a TypeException error
```

If not provided, its type will be deduced from the initial value.

```kaoscript
var mut name = 'John'
// type: String

name = 42
// throws a TypeException error
```

## var dyn

With `var dyn`, the variable is `dynamic` (mutable, no type).

### Description

```kaoscript
var dyn name = 'John'
```

`var dyn` declares the variable `name` and it's rebindable.

```kaoscript
var dyn name = 'John'

name = 42
// no error
```

## var late

### Description

`var late` allows the assignment of an immutable variable's value to be deferred.

The variable can't be accessed before being assigned.

The assignment can only be in a `if/else` statement or a `swicth` statement with a default case.

```kaoscript
var late x

if test() {
	x = 42
}
else {
	x = 'quxbaz'
}
// type of x: Number | String

console.log(x)
```

## assignment

Like JavaScript, kaoscript can create variables with assignments:

```kaoscript
name = 'John'
```

But unlike JavaScript, in kaoscript, those variables are created only inside the current block.

```kaoscript
func foobar() {
	x = 42

	console.log(x)
}

foobar()
// 42

console.log(x)
// throw the ReferenceException error: "x" is not defined
```

### no-undefined

You can disable the creation of variables via assignments, with the attribute `#[rules(no-undefined)]`

```kaoscript
#![rules(no-undefined)]

func foobar() {
	x = 42
	// throw the ReferenceException error: "x" is not defined

	console.log(x)
}
```

## naming

Unlike JavaScript, an identifier is limited to ascii characters and `_` and `$` are reserved keywords.

### Syntax

![naming](@images/guide/variable-declarations/naming.svg)

## reserved keywords

The following keywords are reserved:
- `false`
- `im` [**i** constant of an imaginary number](https://en.wikipedia.org/wiki/Imaginary_number)
- `Infinity`
- `NaN`
- `null`
- `this`
- `true`

## syntax

```syntax
var (*varname* | *destructuring*)[: *type*] = *expression*
var {
	(
		(*varname* | *destructuring*)[: *type*] = *expression*
	)+
}
var *varname1*[: *type1*], *varname2*[: *type2*] = await *expression*

var mut (*varname* | *destructuring*)[*vartype*] = *expression*
var mut *varname1*: *type1*, ..., *varnameN*: *typeN*
var mut {
	(
		(*varname* | *destructuring*)[*vartype*] = *expression*
		|
		*varname*: *type*
	)+
}
var mut *varname1*[*vartype1*], *varname2*[*vartype2*] = await *expression*

var dyn (*varname* | *destructuring*) = *expression*
var dyn *varname1*, ..., *varnameN*
var dyn {
	(
		(*varname* | *destructuring*) = *expression*
		|
		*varname*
	)+
}
var dyn *varname1*, *varname2* = await *expression*

var late *varname1*[*vartype1*], ..., *varnameN*[*vartypeN*]
var late {
	(
		*varname*[*vartype*]
	)+
}


vartype = (: *type* | ?)
```

|            | immutable | typed | lateinit |
| :--------: | :-------: | :---: | :------: |
|   `var`    |    ✅     |  ✅   |    ❌    |
| `var mut`  |    ❌     |  ✅   |    ❌    |
| `var dyn`  |    ❌     |  ❌   |    ❌    |
| `var late` |    ✅     |  ✅   |    ✅    |
