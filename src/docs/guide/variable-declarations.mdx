---
name: Variable Declarations
menu: Guide
route: /guide/variable-declarations
---

# Variable Declarations


```syntax
var (*varname* | *destructuring*)[: *type*] = *expression*
var mut (*varname* | *destructuring*)[(: *type* | ?)] = *expression*
var mut *varname1*: *type*1[, *varname2*: *type*2 ... [, *varnameN*: *type*N]]
var dyn (*varname* | *destructuring*) = *expression*
var dyn *varname1*[, *varname2* ... [, *varnameN*]]
var late *varname1*[: *type*1][, *varname2*[: *type*2] ... [, *varnameN*[: *type*N]]]
```

|            | immutable | typed | lateinit |
| :--------: | :-------: | :---: | :------: |
|   `var`    |    ✅     |  ✅   |    ❌    |
| `var mut`  |    ❌     |  ✅   |    ❌    |
| `var dyn`  |    ❌     |  ❌   |    ❌    |
| `var late` |    ✅     |  ✅   |    ✅    |


|                          | immutable | typed | inferred | assignment |                    |
| :----------------------: | :-------: | :---: | :------: | :--------: | :----------------: |
|         `let x`          |    ❌     |  ❌   |    ❌    |     ❌     |    `var dyn x`     |
|      `let x: Type`       |    ❌     |  ✅   |    ❌    |     ❌     | `var mut x: Type`  |
|         `auto x`         |    ❌     |  ✅   |    ✅    |     ✅     |    `var mut x`     |
|        `const x`         |    ✅     |  ✅   |    ✅    |     ✅     |      `var x`       |
|     `const x: Type`      |    ✅     |  ✅   |    ❌    |     ✅     |   `var x: Type`    |
|    `lateinit const x`    |    ✅     |  ✅   |    ✅    |     ❌     |    `var late x`    |
| `lateinit const x: Type` |    ✅     |  ✅   |    ❌    |     ❌     | `var late x: Type` |


## var

By default, a variable is **immutable**.

### Syntax

```syntax
var (*varname* | *destructuring*)[: *type*] = *expression*
```

### Description

```kaoscript
var name = 'John'
```

`var` declares the variable `name` as **immutable reference**. But the referenced value can be modified.

```kaoscript
var names = ['John']

names.push('Jane')
// names: ['John', 'Jane']

// but
names = ['John']
// will through an error
```

### Typing

If not type is provided, `var` will deduce the variable's type based on its value.

```kaoscript
var name = 'John'
// type: String

var names: Array = []
// type: Array
```

The type of a variable declared with `var` can't be changed.

## var mut

With `var mut`, the variable is **mutable** but still **typed**

### Syntax

```syntax
var mut (*varname* | *destructuring*)[: *type*] = *expression*
var mut *varname1*: *type*1[, *varname2*: *type*2 ... [, *varnameN*: *type*N]]
```

### Typing

If a type is provided at the declaration of a variable, its type can't be changed.

```kaoscript
var mut name: String = 'John'

name = 42
// throws a TypeException error
```

If not provided, its type will be deduced from the initial value.

```kaoscript
var mut name = 'John'
// type: String

name = 42
// throws a TypeException error
```

## var dyn

With `var dyn`, the variable is `dynamic` (mutable, no type).

### Syntax

```syntax
var dyn (*varname* | *destructuring*) = *expression*
var dyn *varname1*[, *varname2* ... [, *varnameN*]]
```

### Description

```kaoscript
var dyn name = 'John'
```

`var dyn` declares the variable `name` and it's rebindable.

```kaoscript
var dyn name = 'John'

name = 42
// no error
```

## var late

### Syntax

```syntax
var late *varname1*[: *type*1][, *varname2*[: *type*2] ... [, *varnameN*[: *type*N]]]
```

### Description

`var late` allows the assignment of an immutable variable's value to be deferred.

The variable can't be accessed before being assigned.

The assignment can only be in a `if/else` statement or a `swicth` statement with a default case.

```kaoscript
var late x

if test() {
	x = 42
}
else {
	x = 'quxbaz'
}
// type of x: Number | String

console.log(x)
```

## assignment

Like JavaScript, kaoscript can create variables with assignments:

```kaoscript
name = 'John'
```

But unlike JavaScript, in kaoscript, those variables are created only inside the current block.

```kaoscript
func foobar() {
	x = 42

	console.log(x)
}

foobar()
// 42

console.log(x)
// throw the ReferenceException error: "x" is not defined
```

### no-undefined

You can disable the creation of variables via assignments, with the attribute `#[rules(no-undefined)]`

```kaoscript
#![rules(no-undefined)]

func foobar() {
	x = 42
	// throw the ReferenceException error: "x" is not defined

	console.log(x)
}
```

## naming

Unlike JavaScript, an identifier is limited to ascii characters and `_` and `$` are reserved keywords.

### Syntax

![naming](@images/guide/variable-declarations/naming.svg)
