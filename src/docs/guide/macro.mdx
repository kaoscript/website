---
name: Macro
menu: Guide
route: /guide/macro
---

# Macro

In kaoscript, macros are executed when the modules are compiled. They are inspired by Rust and Haxe.
They are working on AST but are generating kaoscript code.

## syntax

```syntax
macro *varname*([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]]) {
	*statement*
	macro *macro-line*
	macro {
		*...macro-lines*
	}
}
macro *varname*([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]]) => *macro-line*

macro-line = *text* | macro-expression
macro-expression = "#" [(a|e|j|s|w)] "(" *expression* ")")
```

## quick example

```kaoscript
macro trace_build_age() {
	var d = new Date(2013, 2, 15)

	var buildTime = Math.floor(d.getTime() / 1000)

	macro {
		var runTime = Math.floor(Date.now() / 1000)
		var age = runTime - #(buildTime)

		console.log(`Right now it's \(runTime), and this build is \(age) seconds old`)
	}
}

trace_build_age!()
```

## parameters

The parameters are AST and can be typed as:

- `Array`
- `Boolean`
- `Dictionary`
- `Expression`
- `Identifier`
- `Number`
- `String`

## auto-evaluated parameter

A parameter can also be a typical kaoscript value. It need to be prefixed with an at sign (`@`).

```kaoscript
macro foobar(@x: Array) {
	macro #(x.length)
}
```

## expression reification

AST values aren't ignoring any reification.

| reification |   reification    |                      description                      |
|:-----------:|:----------------:|:-----------------------------------------------------:|
|  argument   |    `#a(...)`     |             generates a list of arguments             |
| expression  | `#e(...)` `#...` |     generates an expression from the given value      |
|    join     |  `#j(..., sep)`  | generates a list separated by the specified separator |
|  statement  |    `#s(...)`     |            generates a list of statements             |
|    write    |    `#w(...)`     |                  write out the value                  |

### examples

|    type    |  argument   | expression  |    join     |  statement  |    write    |
|:----------:|:-----------:|:-----------:|:-----------:|:-----------:|:-----------:|
|  Boolean   |   `true`    |   `true`    |   `true`    |   `true`    |   `true`    |
| Dictionary | `{x: '42'}` | `{x: '42'}` | `{x: '42'}` | `{x: '42'}` | `{x: '42'}` |
|   Number   |   `3.14`    |   `3.14`    |   `3.14`    |   `3.14`    |   `3.14`    |
|   String   |  `foobar`   | `'foobar'`  |  `foobar`   |  `foobar`   |  `foobar`   |

### array

```kaoscript
#a(['foobar()', 'quxbaz()'])

// generates:
foobar(), quxbaz()
```

```kaoscript
#e(['foobar()', 'quxbaz()'])

// generates:
['foobar()', 'quxbaz()']
```

```kaoscript
#j(['foobar()', 'quxbaz()'], ' && ')

// generates:
foobar() && quxbaz()
```

```kaoscript
#s(['foobar()', 'quxbaz()'])

// generates:
foobar()
quxbaz()
```

```kaoscript
#w(['foobar()', 'quxbaz()'])

// generates:
foobar()quxbaz()
```

## overloading

kaoscript supports macro overloading.

```kaoscript
macro match_tokens(a) => 'any'
macro match_tokens(a: Identifier) => 'identifier'
macro match_tokens(a: Number) => 'number'

console.log(match_tokens!(a))
// console.log("identifier")

console.log(match_tokens!(42))
// console.log("number")

console.log(match_tokens!('foobar'))
// console.log("any")

console.log(match_tokens!(1 + 1))
// console.log("any")
```

## examples

```kaoscript
macro times_five(e) => 5 * #(e)

macro times_five_bb(e) {
	macro {
		5 * #(e)
	}
}

macro times_five_bl(e) {
	macro 5 * #(e)
}

console.log(times_five!(42))
// console.log(5 * 42)

console.log(times_five!(x * y))
// console.log(5 * x * y)
```

```kaoscript
export enum Space<string> {
	RGB
	SRGB
}

export class Color {
	macro registerSpace(@space: Object) {
		if space.components? {
			var fields: Array = []
			var methods: Array = []

			for var component, name of space.components {
				var field = `_\(name)`

				fields.push(macro private #w(field): Number)

				methods.push(macro {
					override #w(name)() => this.getField(#(name))
					override #w(name)(value) => this.setField(#(name), value)
				})
			}

			macro {
				Color.registerSpace(#(space))

				impl Color {
					#s(fields)
					#s(methods)
				}
			}
		}
		else {
			macro Color.registerSpace(#(space))
		}
	}
}

Color.registerSpace!({
	name: Space::SRGB
	alias: [Space::RGB]
	formatters: {
		hex(that: Color): string { // {{{
			return $hex(that)
		} // }}}
		srgb(that: Color): string { // {{{
			if that._alpha == 1 {
				return `rgb(\(that._red), \(that._green), \(that._blue))`
			}
			else {
				return `rgba(\(that._red), \(that._green), \(that._blue), \(that._alpha))`
			}
		} // }}}
	}
	components: {
		red: {
			max: 255
		}
		green: {
			max: 255
		}
		blue: {
			max: 255
		}
	}
})
```
