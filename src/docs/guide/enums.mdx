---
name: Enums
menu: Guide
route: /guide/enums
---

# Enums

## syntax

```syntax
enum *varname*["<" *type* ">"] {
	(
		*varname* [= *value*]
		|
		*method*
	)*
}

bitflag *varname* {
	(
		*varname* [= *value*]
		|
		*method*
	)*
}

method = [async] *varname*([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]])[: *type* | auto] [~ *class1* [, *class2* ... [, *classN*]]] [ => *expression* | {
	*...statements*
}]
```

## number

```kaoscript
enum Color {
	Red
	Green
	Blue
}

let color = Color::Red

console.log(color)
// 0
```

### custom start
```kaoscript
enum Color {
	Red = 100
	Green		// 101
	Blue		// 102
}
```

### custom number
```kaoscript
enum Color {
	Red		= 100
	Green	= 200
	Blue	= 300
}
```

## string

```kaoscript
enum CardSuit<String> {
	Clubs
	Diamonds
	Hearts
	Spades
}

let card = CardSuit::Clubs

console.log(card)
// clubs
```

### custom string

```kaoscript
enum CardSuit<String> {
	Clubs		= 'clb'
	Diamonds	= 'dmd'
	Hearts		= 'hrt'
	Spades		= 'spd'
}
```

## flags

```kaoscript
bitflag AnimalFlags {
	None
	HasClaws
	CanFly
	EatsFish
	Endangered

	EndangeredFlyingClawedFishEating = HasClaws + CanFly + EatsFish + Endangered
	Predator = CanFly + HasClaws
}

func printAnimalAbilities(animal) {
	let animalFlags = animal.flags

	if animalFlags ~~ AnimalFlags::HasClaws {
		console.log('animal has claws')
	}

	if animalFlags ~~ AnimalFlags::CanFly {
		console.log('animal can fly')
	}

	if animalFlags == AnimalFlags::None {
		console.log('nothing')
	}
}

let animal = {
	flags: AnimalFlags::None
}

printAnimalAbilities(animal)
// nothing

animal.flags += AnimalFlags::HasClaws
printAnimalAbilities(animal)
// animal has claws

animal.flags -= AnimalFlags::HasClaws
printAnimalAbilities(animal)
// nothing

animal.flags += AnimalFlags::HasClaws + AnimalFlags::CanFly
printAnimalAbilities(animal)
// animal has claws, animal can fly
```

## methods

The methods are always static and public.

A method can be called as an instance method if its first argument's type is of the enum. In this case, the first argument must be omitted.
The call will be rewrited to use the instance as the first argument of the method.

### example

```kaoscript
enum Weekday {
	MONDAY
	TUESDAY
	WEDNESDAY
	THURSDAY
	FRIDAY
	SATURDAY
	SUNDAY

	isWeekend(day: Weekday): Boolean => day == SATURDAY + SUNDAY

	fromString(value: String): Weekday? {
		switch value {
			'monday' => return MONDAY
		}

		return null
	}
}

func foobar(day: Weekday) {
	if Weekday.isWeekend(day) {
	}

	if day.isWeekend() {
	}
}

foobar(Weekday.fromString('monday'))
```