---
name: Enums
menu: Guide
route: /guide/enums
---

# Enums

## syntax

```syntax
enum *varname*["<" *entity-type* ">"] {
	(
		*varname* [= *expression*]
		|
		*method*
	)*
}

bitmask *varname*["<" [(u8 | u16 | u32 | u48 | u64 | u128 | u256)]  ">"] {
	(
		*varname* [= *expression*]
		|
		*method*
	)*
}

method = [async] *varname*([[*parameter1*] [, [*parameter2*] ... [, [*parameterN*]]])[: *type* | auto] [~ *class1* [, *class2* ... [, *classN*]]] [ => *expression* | {
	*...statements*
}]
```

## number

```kaoscript
enum Color {
	Red
	Green
	Blue
}

var color = Color::Red

console.log(color)
// 0
```

### custom start
```kaoscript
enum Color {
	Red = 100
	Green		// 101
	Blue		// 102
}
```

### custom number
```kaoscript
enum Color {
	Red		= 100
	Green	= 200
	Blue	= 300
}
```

## string

```kaoscript
enum CardSuit<String> {
	Clubs
	Diamonds
	Hearts
	Spades
}

var card = CardSuit::Clubs

console.log(card)
// clubs
```

### custom string

```kaoscript
enum CardSuit<String> {
	Clubs		= 'clb'
	Diamonds	= 'dmd'
	Hearts		= 'hrt'
	Spades		= 'spd'
}
```

## methods

```kaoscript
enum Weekday {
    MONDAY
    TUESDAY
    WEDNESDAY
    THURSDAY
    FRIDAY
    SATURDAY
    SUNDAY

    static fromString(value: String): Weekday? {
        switch value {
            'monday' => return MONDAY
        }

        return null
    }

    isWeekend(): Boolean => this == SATURDAY | SUNDAY
}
```

## bitmask

The default, the bitmask's properties are:
- length: `u16`
- initial value: `0`

The lengths `u64`, `u128` and `u256` aren't implemented yet.

```kaoscript
bitmask AnimalFlags {
	None
	HasClaws
	CanFly
	EatsFish
	Endangered

	EndangeredFlyingClawedFishEating = HasClaws + CanFly + EatsFish + Endangered
	Predator = CanFly + HasClaws
}

func printAnimalAbilities(animal) {
	var animalFlags = animal.flags

	if animalFlags ~~ AnimalFlags::HasClaws {
		console.log('animal has claws')
	}

	if animalFlags ~~ AnimalFlags::CanFly {
		console.log('animal can fly')
	}

	if animalFlags == AnimalFlags::None {
		console.log('nothing')
	}
}

var animal = {
	flags: AnimalFlags::None
}

printAnimalAbilities(animal)
// nothing

animal.flags += AnimalFlags::HasClaws
printAnimalAbilities(animal)
// animal has claws

animal.flags -= AnimalFlags::HasClaws
printAnimalAbilities(animal)
// nothing

animal.flags += AnimalFlags::HasClaws + AnimalFlags::CanFly
printAnimalAbilities(animal)
// animal has claws, animal can fly
```

## methods

The methods are always static and public.

A method can be called as an instance method if its first argument's type is of the enum. In this case, the first argument must be omitted.
The call will be rewrited to use the instance as the first argument of the method.

### example

```kaoscript
enum Weekday {
	MONDAY
	TUESDAY
	WEDNESDAY
	THURSDAY
	FRIDAY
	SATURDAY
	SUNDAY

	isWeekend(day: Weekday): Boolean => day == SATURDAY + SUNDAY

	fromString(value: String): Weekday? {
		switch value {
			'monday' => return MONDAY
		}

		return null
	}
}

func foobar(day: Weekday) {
	if Weekday.isWeekend(day) {
	}

	if day.isWeekend() {
	}
}

foobar(Weekday.fromString('monday'))
```
