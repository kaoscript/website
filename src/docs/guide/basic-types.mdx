---
name: Basic Types
menu: Guide
route: /guide/basic-types
---

# Basic Types

## boolean

The boolean literals are `true` and `false`.


```kaoscript
var isDone = true
```

## number

Numbers are floating point values like in JavaScript but with more possible literals.

`_` can be used to make to number more readable.
An unit can also be added at the end of the number.

> :construction: kaoscript will improve its support of traditional number systems to avoid the caveats of JavaScript numbers.

### decimal

```kaoscript
var int = 6
var time = 10_000_ms
var float = 3.14
var group = 123_456.789_012
var scientific = -7.2e+3		//-7200
var dot = .789_012
var dotScientific = .1E4		// 1000
```

![decimal-regex](@images/guide/basic-types/decimal.svg)

### binary

```kaoscript
var binary1 = 0b1010
var binary2 = 0b1010010101
```

![binary-regex](@images/guide/basic-types/binary.svg)

### hexadecimal

kaoscript supports hexadecimal floating literals introduced in [C99](https://en.cppreference.com/w/c/language/floating_constant) or [C++ 17](https://en.cppreference.com/w/cpp/language/floating_literal)

```kaoscript
var hex = 0xf00d
var group = 0x1234_5678
var float1 = 0x10.1p0			// 16.0625
var float2 = 0x1.2p3			// 9
var float3 = 0x3.14p0			// 3.078125
var float4 = 0xa.bp10			// 10944
```

![hex-regex](@images/guide/basic-types/hex.svg)

### octal

Octal literals are extented with same floating literal as the hexadecimal literals.

```kaoscript
var octal = 0o744
```

![octal-regex](@images/guide/basic-types/octal.svg)

### radix

Radix literals allow you to define you own radix.

```kaoscript
var radix36 = 36rNFfdH45
var radix7 = 7r66.6
```

![radix-regex](@images/guide/basic-types/radix.svg)

### character

```kaoscript
var ch = 0'a'		// 97
```

![character-regex](@images/guide/basic-types/character.svg)

### NaN & Infinity

```kaoscript
if x == NaN {

}

if y == Infinity {

}
```

## string

```kaoscript
var singleQuote = 'John'
var doubleQuote = "Jane"
```

### escape sequences

| Escape sequence          | Character represented                              |
| :----------------------- | :------------------------------------------------- |
| `\0`                     | null character (U+0000)                            |
| `\'`                     | single quote (U+0027)                              |
| `\"`                     | double quote (U+0022)                              |
| `\\`                     | backslash (U+005C)                                 |
| `\n`                     | newline (U+000A)                                   |
| `\r`                     | carriage return (U+000D)                           |
| `\v`                     | vertical tab (U+000B)                              |
| `\t`                     | tab (U+0009)                                       |
| `\b`                     | backspace (U+0008)                                 |
| `\f`                     | form feed (U+000C)                                 |
| `\uXXXX`                 | Unicode code point between `U+0000` and `U+FFFF`   |
| `\u{X}` ... `\u{XXXXXX}` | Unicode code point between `U+0000` and `U+10FFFF` |
| `\xXX`                   | Unicode code point between `U+0000` and `U+00FF`   |

## array

```kaoscript
var array = [1, 2, 3]
```

You can also define an array with a range.

```kaoscript
var a = [1..5]
// 1, 2, 3, 4, 5

var b = [1..<5]
// 1, 2, 3, 4

var c = [1<..5]
// 2, 3, 4, 5

var d = [1<..<5]
// 2, 3, 4

var e = [1..6..2]
// 1, 3, 5

var f = [1<..<6..2]
// 3, 5

var g = [5..1]
// 5, 4, 3, 2, 1

var h = [5..1..2]
// 5, 3, 1

var i = [1..2..0.3]
// 1.0, 1.3, 1.6, 1.9

var i = [1..2..0.2]
// 1.0, 1.2, 1.4, 1.6, 1.8, 2.0
```

```kaoscript
var min = 1
var max = 5

var a = [min..max]
// 1, 2, 3, 4, 5
```

## object

Unlike JavaScript, in kaoscript, a empty object doesn't contain any default properties (no `toString()`, ...).

```kaoscript
var foo = {}
// {}

var bar = {
	bar: 'hello',
	'qux': 'world'
}
// {"bar": "hello", "qux": "world"}
```

### shorthand

```kaoscript
var x = 1
var y = 2
var z = 3

var foo1 = {x, y, z}
// {"x": "1", "y": "2", "z": "3"}

var foo2 = {
	x,
	y,
	z
}
// {"x": "1", "y": "2", "z": "3"}

var foo3 = {
	x
	y
	z
}
// {"x": "1", "y": "2", "z": "3"}
```

### computed & template

```kaoscript
var bar = 'x'

var foo1 = {
	[bar]: 42
}
// {"x": 42}

var foo2 = {
	`\(bar)`: 42
}
// {"x": 42}
```

## regex

kaoscript uses the RegExp class from JavaScript.

```kaoscript
var r = /foobar/gim
```

## null

```kaoscript
var n = null
```


## template string

```kaoscript
var message = 'Hello!'
var name = 'John'

var template = `\(message)\nIt's nice to meet you, \(name).`
// Hello! \nIt's nice to meet you, John.
```

## multi-lines strings

### non-interpolated

```kaoscript
var value = '''
The jury all wrote down on their slates, "SHE doesn't
believe there's an atom of meaning in it," but none of them
attempted to explain the paper.
'''
```

```kaoscript
var value = """

	This string :
	- starts with a line break;
	- ends with a line break;
	- contains no leading withspaces.

	"""
```

### interpolated

~~~kaoscript
var xml = ```
	<book id="bk\(id)">
		<author>\(author)</author>
		<title>\(title)</title>
		<genre>\(genre)</genre>
		<price>\(price)</price>
	</book>
	```
~~~

```kaoscript
var xml = ~~~
	<book id="bk\(id)">
		<author>\(author)</author>
		<title>\(title)</title>
		<genre>\(genre)</genre>
		<price>\(price)</price>
	</book>
	~~~
```
