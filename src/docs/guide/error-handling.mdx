---
name: Error Handling
menu: Guide
route: /guide/error-handling
---

# Error Handling

In kaoscript, the error handling is very Java-like with some go-iness.

## try..catch

### syntax

```syntax
try {
	*...statements*
}
[on *classname1* [catch *varname*]] {
	*...statements*
}]
[on *classname2* [catch *varname*]] {
	*...statements*
}]
...
[on *classnameN* [catch *varname*]] {
	*...statements*
}]
[catch [*varname*] {
	*...statements*
}]
[finally {
	*...statements*
}]
```

### examples

```kaoscript
try {
	console.log('foobar')
}
on RangeError catch error {
	console.log('RangeError', error)
}
catch error {
	console.log('Error', error)
}
finally {
	console.log('finally')
}
```

```kaoscript
try {
	console.log('foobar')
}
on RangeError {
	console.log('RangeError')
}
catch {
	console.log('Error')
}
```

## try expression

```kaoscript
func foobar(x) ~ Error {
    if x {
        return 42
    }
    else {
        throw new Error('foobar')
    }
}

var x = try foobar(false)

var y = try foobar(false) ~ 24

var z = try foobar(false) ?? 24

var a = try! foobar(true)
```

### try

```kaoscript
var x = try foobar(false)
```

If `foobar()` throws an error, then `try` catch it and return `null`

### error coalescing

```kaoscript
var y = try foobar(false) ~ 24
```

If `foobar()` throws an error, then `try` catch it and return the default value `24`

### null coalescing

```kaoscript
var z = try foobar(false) ?? 24
```

`try foobar(false) ?? 24` is equivalent to `(try foobar(false)) ?? 24`.
So if `foobar()` throws an error or return `null`, then the null coalescing failed and return `24`.

### forced-unwraping

```kaoscript
var a = try! foobar(true)
```

`try!` disables the error propagation. If an error actually is thrown, youâ€™ll get a runtime error.
It should be used only when you are sure that no errors are generated.

### conditional

```kaoscript
if try foobar(true) {
    // no error has occured
}
else {
    // an error has occured
}
```

When a `try` expression is casted to a Boolean, it returns `true` when no errors are thrown. If not, it returns `false`.

## disabling

koascript allows to disable the disable the default error handling.

### global

```kaoscript
#![error(off)]

func foobar() ~ Error {
	throw new Error(`Not Implemented`)
}

func disabled() {
	foobar()
}
```

### local

```kaoscript
func foobar() ~ Error {
	throw new Error(`Not Implemented`)
}

#[error(off)]
func qux() {
	foobar()
}
```

The error handling is only disabled for the function `qux`.

## filtering

You can also exclude error type that need to be handled.

```kaoscript
#[error(ignore(SyntaxError))]

#[error(ignore(SyntaxError, UnexpectedError))]
```
