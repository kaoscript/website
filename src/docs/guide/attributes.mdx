---
name: Attributes
menu: Guide
route: /guide/attributes
---

# Attributes

An attribute is metadata applied to the file or to a statement. Attributes are modeled on attributes in Rust

Inner attributes, written with a bang (`!`) after the hash (`#`), apply to the item that the attribute is declared within. Outer attributes, written without the bang after the hash, apply to the statement that follows the attribute.

## syntax

```syntax
inner = "#![" *attributes* "]"

outer = "#[" *attributes* "]"

attributes = (
	*varname* [= *value*]
	|
	*varname* "(" *attribute1*, *attribute2*, ..., *attributeN* ")"
)

value = ( *number* | " *string* ")
```

## binary

Unlike JavaScript, by default, a kaoscript file is a module.
A module can not be executed by the command line `kaoscript`, it will need to be imported from another file.

For a kaoscript file to be executed as binary, it would need the attribute `#![bin]`.

```kaoscript
#![bin]

extern console

console.log('Hello World!')
```

## conditional compilation

Condition compilation allows to transpile the code depending of the targeted engine.

```kaoscript
// if the engine is IE (*trident*) or Safari 8 or under (*lte(jsc-v8)*)
#[if(any(trident, lte(jsc-v8)))]
impl String {
	endsWith(value: String): Boolean => this.length >= value.length && this.slice(this.length - value.length) == value
}

#[else]
disclose String {
	endsWith(search: String): Boolean
}
```

By default, the only available targets are *ecma-v5* and *ecma-v6*.
[@kaoscript/target-commons]() provides those additional targets.

### operators

|         operator          |              description               |
|:-------------------------:|:--------------------------------------:|
| `any`(item1, ..., itemN)  |   true if at least one item is true    |
| `all`(item1, ..., itemN)  |     true if all the items are true     |
| `none`(item1, ..., itemN) |   true if none of the item are true    |
| `one`(item1, ..., itemN)  | true if only and only one item is true |
|        `lt`(item)         |               less than                |
|        `lte`(item)        |          less than or equals           |
|        `gt`(item)         |              greater than              |
|        `gte`(item)        |         greater than or equals         |


## rules

kaoscript allows to apply some rules about the code.

|            rule            |                                        desciption                                        |
|:--------------------------:|:----------------------------------------------------------------------------------------:|
|        no-undefined        |                           all variables must have been defined                           |
|       non-exhaustive       | the declaration is not exhaustive;<br />access to unmatched members don't throw an error |
|       ignore-misfit        |                   accessing an unmatched member doesn't throw an error                   |
|     dont-ignore-misfit     |                      accessing an unmatched member throws an error                       |
|      assert-override       |              assert that overriding methods are matching an existing method              |
|    dont-assert-override    |                overriding methods don't need to match an existing method                 |

### assert-override

`assert-override`/`dont-assert-override` enables/disables all the assertions for overriding an existing method.

### no-undefined

By default, undefined variables assigned will be created.

```kaoscript
name = 'John'
// like: var name = 'John'
```

You can change this behaviour with the rule `no-undefined`.

```kaoscript
#![rules(no-undefined)]

func foobar() {
	x = 42
	// throw the ReferenceException error: "x" is not defined

	console.log(x)
}
```

### non-exhaustive

The rule `non-exhaustive` can only be applied to external declarations or to the statement `disclose`.

By default, external declarations without any definitions are considered as `non-exhaustive`, whereas those with a definition are considired as `exhaustive`.
The statement `disclose` forces the associated declaration to be considired as `exhaustive`.

```kaoscript
extern namespace console
// console: non-exhaustive

console.log('foobar')
```

```kaoscript
extern namespace console {
	func log(...)
}
// console: exhaustive

console.log('foobar')
// print: foobar

console.debug('foobar')
// throw an error
```

```kaoscript
extern class Foobar
// Foobar: non-exhaustive

Foobar.log('foobar')
```

```kaoscript
extern class Foobar
// Foobar: non-exhaustive

disclose Foobar {
	log(...)
}
// Foobar: exhaustive

console.log('foobar')
// print: foobar

console.debug('foobar')
// throw an error
```

### ignore-misfit

By default, if a member (field/method for a class, variable/function for a namespace or field for an enum) or a function is accessed and can't be matched with the given arguments, it will throw an error.

`ignore-misfit` disables this behaviour and ignores mismatched members or functions.
`dont-ignore-misfit` re-enables this behaviour.

## runtime

kaoscript needs a runtime to add dynamics functions on classes (`Helper`), to do type checking (`Type`), to do arithmetic operations (`Operator`).

The runtime is imported only when it's needed.

The default runtime is [@kaoscript/runtime]() and provides only the bare minimum.

But you can provide your own runtime with the following attributes

```kaoscript
#![runtime(package="@kaoscript/runtime")]
#![runtime(type(alias="Type", member="Type"))]
#![runtime(helper(alias="Helper", member="Helper"))]
#![runtime(prefix='KS')]
```
