---
name: Import & Export
menu: Guide
route: /guide/import-export
---

# Import & Export

## import

`import` is used to import bindings which are exported by another module.

### syntax

```syntax
 import "*path*" (
	{
		declarator+
	}
	|
	declarator
)

declarator = ['(' *parameter1*, ..., *parameterN* ')' | '(...)'] (
	for (*varname1*[: *type*] | *type*) [=> *varname1*], ..., (*varnameN*[: *type*] | *type*) [=> *varnameN*]
	|
	but *varname1*, ..., *varnameN*
	|
	=> *varname* [{
		(
			(*varname*[: *type*] | *type*) [=> *varname*] [,]
		)+
	}]
	|
	{
		(
			(*varname*[: *type*] | *type*) [=> *varname*] [,]
			|
			\* => *varname* [,]
		)+
	}
)

parameter = ([*module-varname*:] *expression* | require [*module-varname*:] *varname*)
```

### examples

```kaoscript
import 'chai' for expect

import 'foobar.ks' for name => foo

import 'quxbaz'(foo)
```

```kaoscript
import 'color' for Color => C, Space => S
// variables: C, S

import 'color.srgb'(Color: C, Space: S) {
	* => T

	Color => C
	Space => S
}
// variables: C, S, T
// C == T.Color
// S == T.Space
```

```kaoscript
import 'parser' {
	func createToken
	sealed class Lexer
	sealed class Parser
	sealed class Token
}
// functions: createToken
// classes: Lexer, Parser, Token

import 'parser' => CT {
	func createToken
	sealed class Lexer
	sealed class Parser
	sealed class Token
}
// namespace: CT
// functions: CT.createToken
// classes: CT.Lexer, CT.Parser, CT.Token
```

### unnamed JavaScript module

Unnamed import will generate a *variable* whose name will based on the module name.

```kaoscript
import 'fs'
// variable: fs

import 'klaw-sync'
// variable: klawSync
```

### parameters

When importing a kaoscript module, this module might require some parameters (defined with the statement `require`).

Those parameters are given like:

```kaoscript
class Color {
}

class Space {
}

import 'color.srgb'(Color, Space)
```

It's supporting named parameters, so you can do:

```kaoscript
class MyColor {
}

class MySpace {
}

import 'color.srgb'(Color: MyColor, Space: MySpace)
```

If any parameter is missing or doesn't match, kaoscript will throw an error.

### require modifier

`require` is a modifier for import parameter.

```kaoscript
import 'moduleX'(require ClassY)
```

kaoscript will *require* the class `ClassY` with the same type as expected by the module `moduleX`.

### autofill parameters

```kaoscript
import '@zokugun/lang'
import '@zokugun/lang.color'(...)
```

`(...)` indicates to kaoscript that it needs to find the matching parameters from availables variables.

## export

`export` is used when creating modules to export functions, objects, or primitive values from the module so they can be used by other programs with the `import` statement.

### syntax

```syntax
 export (
	\* [but (*varname1*, ..., *varnameN* | {
		(*varname*)+
	})]
	|
	{
		*declarator*+
	}
	|
	*declarator*
)

declarator = (
	*statement*
	|
	*varname* [(
		'.\*'
		|
		=> *varname*
		|
		for (
			'\*'
			|
			{
				(*varname* [=> *alias*][,])+
			}
			|
			*varname1* [=> *alias1*], ..., *varnameN* [=> *aliasN*]
		)
	)]
)
```

### examples

```kaoscript
var PI = 3.14

export PI
```

```kaoscript
export enum Space<String> {
	RGB
	SRGB
}
```

```kaoscript
namespace NS {
	export {
		func foo() {
		}

		func bar() {
		}

		func qux() {
		}
	}
}
export NS for foo, bar, qux
```
